buildscript {
    ext.kotlin_version = '1.8.0'
    ext.webrtc_version = '1.0.32006'
    ext.location_version = '19.0.1'
    ext.nearby_version = '18.0.2'

    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.5'
        classpath 'de.undercouch:gradle-download-task:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // Other dependencies can be added here

    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task checkStyle(type: JavaExec) {
    workingDir = "utils"
    classpath = files("utils/google-java-format-1.7-all-deps.jar")
    FileTree tree = fileTree('app/src/main/java') {
        include '**/*.java'
    }
    args(["-n", "--set-exit-if-changed"])
    tree.each { args += it }
    doFirst {
        println("You can fix the style of the files below (if any) with "
                + "\u001B[33m" + "./gradlew applyStyle" + "\u001B[0m")
    }
    doLast {
        println("\u001B[32m" + " -> All java files follow the correct style." + "\u001B[0m")
    }
}

task applyStyle(type: JavaExec) {
    workingDir = "utils"
    classpath = files("utils/google-java-format-1.7-all-deps.jar")
    FileTree tree = fileTree('app/src/main/java') {
        include '**/*.java'
    }
    args("-r")
    tree.each { args += it }
}

apply plugin: 'com.google.gms.google-services'


